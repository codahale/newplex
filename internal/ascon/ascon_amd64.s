//go:build amd64 && !purego

#include "textflag.h"

// ASCON_ROUND(constant)
// R8:  x0
// R9:  x1
// R10: x2
// R11: x3
// R12: x4
// RAX, RBX, RCX, RDX, R13: scratch for s-box
// R15: scratch for linear layer
#define ASCON_ROUND(constant) \
	XORQ $constant, R10 \
	XORQ R12, R8 \
	XORQ R11, R12 \
	XORQ R9, R10 \
	MOVQ R8, RAX; NOTQ RAX; ANDQ R9, RAX \
	MOVQ R9, RBX; NOTQ RBX; ANDQ R10, RBX \
	MOVQ R10, RCX; NOTQ RCX; ANDQ R11, RCX \
	MOVQ R11, RDX; NOTQ RDX; ANDQ R12, RDX \
	MOVQ R12, R13; NOTQ R13; ANDQ R8, R13 \
	XORQ RBX, R8 \
	XORQ RCX, R9 \
	XORQ RDX, R10 \
	XORQ R13, R11 \
	XORQ RAX, R12 \
	XORQ R8, R9 \
	XORQ R12, R8 \
	XORQ R10, R11 \
	NOTQ R10 \
	MOVQ R8, R15; RORQ $19, R15; XORQ R15, R8; RORQ $9, R15; XORQ R15, R8 \
	MOVQ R9, R15; RORQ $61, R15; XORQ R15, R9; RORQ $42, R15; XORQ R15, R9 \
	MOVQ R10, R15; RORQ $1, R15; XORQ R15, R10; RORQ $5, R15; XORQ R15, R10 \
	MOVQ R11, R15; RORQ $10, R15; XORQ R15, R11; RORQ $7, R15; XORQ R15, R11 \
	MOVQ R12, R15; RORQ $7, R15; XORQ R15, R12; RORQ $34, R15; XORQ R15, R12

// func permute12(state *[40]byte)
TEXT ·permute12(SB), NOSPLIT, $0
	MOVQ state+0(FP), DI

	MOVQ 0(DI), R8
	MOVQ 8(DI), R9
	MOVQ 16(DI), R10
	MOVQ 24(DI), R11
	MOVQ 32(DI), R12
	BSWAPQ R8
	BSWAPQ R9
	BSWAPQ R10
	BSWAPQ R11
	BSWAPQ R12

	ASCON_ROUND(0xf0)
	ASCON_ROUND(0xe1)
	ASCON_ROUND(0xd2)
	ASCON_ROUND(0xc3)
	ASCON_ROUND(0xb4)
	ASCON_ROUND(0xa5)
	ASCON_ROUND(0x96)
	ASCON_ROUND(0x87)
	ASCON_ROUND(0x78)
	ASCON_ROUND(0x69)
	ASCON_ROUND(0x5a)
	ASCON_ROUND(0x4b)

	BSWAPQ R8
	BSWAPQ R9
	BSWAPQ R10
	BSWAPQ R11
	BSWAPQ R12
	MOVQ R8, 0(DI)
	MOVQ R9, 8(DI)
	MOVQ R10, 16(DI)
	MOVQ R11, 24(DI)
	MOVQ R12, 32(DI)
	RET

// func permute8(state *[40]byte)
TEXT ·permute8(SB), NOSPLIT, $0
	MOVQ state+0(FP), DI

	MOVQ 0(DI), R8
	MOVQ 8(DI), R9
	MOVQ 16(DI), R10
	MOVQ 24(DI), R11
	MOVQ 32(DI), R12
	BSWAPQ R8
	BSWAPQ R9
	BSWAPQ R10
	BSWAPQ R11
	BSWAPQ R12

	ASCON_ROUND(0xb4)
	ASCON_ROUND(0xa5)
	ASCON_ROUND(0x96)
	ASCON_ROUND(0x87)
	ASCON_ROUND(0x78)
	ASCON_ROUND(0x69)
	ASCON_ROUND(0x5a)
	ASCON_ROUND(0x4b)

	BSWAPQ R8
	BSWAPQ R9
	BSWAPQ R10
	BSWAPQ R11
	BSWAPQ R12
	MOVQ R8, 0(DI)
	MOVQ R9, 8(DI)
	MOVQ R10, 16(DI)
	MOVQ R11, 24(DI)
	MOVQ R12, 32(DI)
	RET
