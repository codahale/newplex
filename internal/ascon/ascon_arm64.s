//go:build arm64 && !purego

#include "textflag.h"

// ASCON_ROUND(constant)
// R1: x0
// R2: x1
// R3: x2
// R4: x3
// R5: x4
// R6, R7, R8, R9, R10: scratch
#define ASCON_ROUND(constant) \
	EOR $constant, R3; \
	EOR R5, R1; \
	EOR R4, R5; \
	EOR R2, R3; \
	BIC R1, R2, R6; \
	BIC R2, R3, R7; \
	BIC R3, R4, R8; \
	BIC R4, R5, R9; \
	BIC R5, R1, R10; \
	EOR R7, R1; \
	EOR R8, R2; \
	EOR R9, R3; \
	EOR R10, R4; \
	EOR R6, R5; \
	EOR R1, R2; \
	EOR R5, R1; \
	EOR R3, R4; \
	MVN R3, R3; \
	ROR $19, R1, R6; ROR $28, R1, R7; EOR R6, R1; EOR R7, R1; \
	ROR $61, R2, R6; ROR $39, R2, R7; EOR R6, R2; EOR R7, R2; \
	ROR $1,  R3, R6; ROR $6,  R3, R7; EOR R6, R3; EOR R7, R3; \
	ROR $10, R4, R6; ROR $17, R4, R7; EOR R6, R4; EOR R7, R4; \
	ROR $7,  R5, R6; ROR $41, R5, R7; EOR R6, R5; EOR R7, R5

// func permute12(state *[40]byte)
TEXT ·permute12(SB), NOSPLIT, $0
	MOVD state+0(FP), R0

	MOVD 0(R0), R1
	MOVD 8(R0), R2
	MOVD 16(R0), R3
	MOVD 24(R0), R4
	MOVD 32(R0), R5
	REV R1, R1
	REV R2, R2
	REV R3, R3
	REV R4, R4
	REV R5, R5

	ASCON_ROUND(0xf0)
	ASCON_ROUND(0xe1)
	ASCON_ROUND(0xd2)
	ASCON_ROUND(0xc3)
	ASCON_ROUND(0xb4)
	ASCON_ROUND(0xa5)
	ASCON_ROUND(0x96)
	ASCON_ROUND(0x87)
	ASCON_ROUND(0x78)
	ASCON_ROUND(0x69)
	ASCON_ROUND(0x5a)
	ASCON_ROUND(0x4b)

	REV R1, R1
	REV R2, R2
	REV R3, R3
	REV R4, R4
	REV R5, R5
	MOVD R1, 0(R0)
	MOVD R2, 8(R0)
	MOVD R3, 16(R0)
	MOVD R4, 24(R0)
	MOVD R5, 32(R0)
	RET

// func permute8(state *[40]byte)
TEXT ·permute8(SB), NOSPLIT, $0
	MOVD state+0(FP), R0

	MOVD 0(R0), R1
	MOVD 8(R0), R2
	MOVD 16(R0), R3
	MOVD 24(R0), R4
	MOVD 32(R0), R5
	REV R1, R1
	REV R2, R2
	REV R3, R3
	REV R4, R4
	REV R5, R5

	ASCON_ROUND(0xb4)
	ASCON_ROUND(0xa5)
	ASCON_ROUND(0x96)
	ASCON_ROUND(0x87)
	ASCON_ROUND(0x78)
	ASCON_ROUND(0x69)
	ASCON_ROUND(0x5a)
	ASCON_ROUND(0x4b)

	REV R1, R1
	REV R2, R2
	REV R3, R3
	REV R4, R4
	REV R5, R5
	MOVD R1, 0(R0)
	MOVD R2, 8(R0)
	MOVD R3, 16(R0)
	MOVD R4, 24(R0)
	MOVD R5, 32(R0)
	RET
