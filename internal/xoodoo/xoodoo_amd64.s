//go:build amd64 && !purego

#include "textflag.h"

#define XOODOO_ROUND(rc) \
	MOVL R8, SI; XORL R12, SI; XORL AX, SI; MOVL SI, 0(SP); \
	MOVL R9, SI; XORL R13, SI; XORL BX, SI; MOVL SI, 4(SP); \
	MOVL R10, SI; XORL R14, SI; XORL CX, SI; MOVL SI, 8(SP); \
	MOVL R11, SI; XORL R15, SI; XORL DX, SI; MOVL SI, 12(SP); \
	MOVL 12(SP), SI; MOVL SI, DI; ROLL $5, SI; ROLL $14, DI; XORL SI, DI; \
	XORL DI, R8; XORL DI, R12; XORL DI, AX; \
	MOVL 0(SP), SI; MOVL SI, DI; ROLL $5, SI; ROLL $14, DI; XORL SI, DI; \
	XORL DI, R9; XORL DI, R13; XORL DI, BX; \
	MOVL 4(SP), SI; MOVL SI, DI; ROLL $5, SI; ROLL $14, DI; XORL SI, DI; \
	XORL DI, R10; XORL DI, R14; XORL DI, CX; \
	MOVL 8(SP), SI; MOVL SI, DI; ROLL $5, SI; ROLL $14, DI; XORL SI, DI; \
	XORL DI, R11; XORL DI, R15; XORL DI, DX; \
	MOVL R15, SI; MOVL R14, R15; MOVL R13, R14; MOVL R12, R13; MOVL SI, R12; \
	ROLL $11, AX; ROLL $11, BX; ROLL $11, CX; ROLL $11, DX; \
	XORL $rc, R8; \
	MOVL R8, SI; MOVL R12, BP; \
	MOVL R12, DI; NOTL DI; ANDL AX, DI; XORL DI, R8; \
	MOVL AX, DI; NOTL DI; ANDL SI, DI; XORL DI, R12; \
	MOVL SI, DI; NOTL DI; ANDL BP, DI; XORL DI, AX; \
	MOVL R9, SI; MOVL R13, BP; \
	MOVL R13, DI; NOTL DI; ANDL BX, DI; XORL DI, R9; \
	MOVL BX, DI; NOTL DI; ANDL SI, DI; XORL DI, R13; \
	MOVL SI, DI; NOTL DI; ANDL BP, DI; XORL DI, BX; \
	MOVL R10, SI; MOVL R14, BP; \
	MOVL R14, DI; NOTL DI; ANDL CX, DI; XORL DI, R10; \
	MOVL CX, DI; NOTL DI; ANDL SI, DI; XORL DI, R14; \
	MOVL SI, DI; NOTL DI; ANDL BP, DI; XORL DI, CX; \
	MOVL R11, SI; MOVL R15, BP; \
	MOVL R15, DI; NOTL DI; ANDL DX, DI; XORL DI, R11; \
	MOVL DX, DI; NOTL DI; ANDL SI, DI; XORL DI, R15; \
	MOVL SI, DI; NOTL DI; ANDL BP, DI; XORL DI, DX; \
	ROLL $1, R12; ROLL $1, R13; ROLL $1, R14; ROLL $1, R15; \
	MOVL AX, SI; MOVL BX, DI; MOVL CX, AX; ROLL $8, AX; MOVL DX, BX; ROLL $8, BX; \
	MOVL SI, CX; ROLL $8, CX; MOVL DI, DX; ROLL $8, DX;

TEXT Â·permute(SB), NOSPLIT, $16
	MOVQ state+0(FP), SI

	MOVL 0(SI), R8
	MOVL 4(SI), R9
	MOVL 8(SI), R10
	MOVL 12(SI), R11
	MOVL 16(SI), R12
	MOVL 20(SI), R13
	MOVL 24(SI), R14
	MOVL 28(SI), R15
	MOVL 32(SI), AX
	MOVL 36(SI), BX
	MOVL 40(SI), CX
	MOVL 44(SI), DX

	XOODOO_ROUND(0x058)
	XOODOO_ROUND(0x038)
	XOODOO_ROUND(0x3c0)
	XOODOO_ROUND(0x0d0)
	XOODOO_ROUND(0x120)
	XOODOO_ROUND(0x014)
	XOODOO_ROUND(0x060)
	XOODOO_ROUND(0x02c)
	XOODOO_ROUND(0x380)
	XOODOO_ROUND(0x0f0)
	XOODOO_ROUND(0x1a0)
	XOODOO_ROUND(0x012)

	MOVQ state+0(FP), SI
	MOVL R8, 0(SI)
	MOVL R9, 4(SI)
	MOVL R10, 8(SI)
	MOVL R11, 12(SI)
	MOVL R12, 16(SI)
	MOVL R13, 20(SI)
	MOVL R14, 24(SI)
	MOVL R15, 28(SI)
	MOVL AX, 32(SI)
	MOVL BX, 36(SI)
	MOVL CX, 40(SI)
	MOVL DX, 44(SI)
	RET
