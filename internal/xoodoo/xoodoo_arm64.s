//go:build arm64 && !purego

#include "textflag.h"

// R1-R4:   A0,0-A3,0
// R5-R8:   A0,1-A3,1
// R9-R12:  A0,2-A3,2
// R13-R16: P0-P3
// R17, R19-R21: E0-E3
// R22-R26: scratch

#define XOODOO_ROUND(rc) \
	EORW R5, R1, R13; EORW R9, R13, R13; \
	EORW R6, R2, R14; EORW R10, R14, R14; \
	EORW R7, R3, R15; EORW R11, R15, R15; \
	EORW R8, R4, R16; EORW R12, R16, R16; \
	RORW $27, R16, R17; RORW $18, R16, R22; EORW R22, R17, R17; \
	RORW $27, R13, R19; RORW $18, R13, R22; EORW R22, R19, R19; \
	RORW $27, R14, R20; RORW $18, R14, R22; EORW R22, R20, R20; \
	RORW $27, R15, R21; RORW $18, R15, R22; EORW R22, R21, R21; \
	EORW R17, R1; EORW R17, R5; EORW R17, R9; \
	EORW R19, R2; EORW R19, R6; EORW R19, R10; \
	EORW R20, R3; EORW R20, R7; EORW R20, R11; \
	EORW R21, R4; EORW R21, R8; EORW R21, R12; \
	MOVD R8, R22; MOVD R7, R8; MOVD R6, R7; MOVD R5, R6; MOVD R22, R5; \
	RORW $21, R9, R9; RORW $21, R10, R10; RORW $21, R11, R11; RORW $21, R12, R12; \
	MOVW $rc, R22; EORW R22, R1; \
	MOVD R1, R22; MOVD R5, R23; MOVD R9, R24; \
	BICW R23, R24, R25; EORW R25, R1; \
	BICW R24, R22, R25; EORW R25, R5; \
	BICW R22, R23, R25; EORW R25, R9; \
	MOVD R2, R22; MOVD R6, R23; MOVD R10, R24; \
	BICW R23, R24, R25; EORW R25, R2; \
	BICW R24, R22, R25; EORW R25, R6; \
	BICW R22, R23, R25; EORW R25, R10; \
	MOVD R3, R22; MOVD R7, R23; MOVD R11, R24; \
	BICW R23, R24, R25; EORW R25, R3; \
	BICW R24, R22, R25; EORW R25, R7; \
	BICW R22, R23, R25; EORW R25, R11; \
	MOVD R4, R22; MOVD R8, R23; MOVD R12, R24; \
	BICW R23, R24, R25; EORW R25, R4; \
	BICW R24, R22, R25; EORW R25, R8; \
	BICW R22, R23, R25; EORW R25, R12; \
	RORW $31, R5, R5; RORW $31, R6, R6; RORW $31, R7, R7; RORW $31, R8, R8; \
	MOVD R9, R22; MOVD R10, R23; \
	RORW $24, R11, R9; RORW $24, R12, R10; RORW $24, R22, R11; RORW $24, R23, R12;

TEXT Â·permute(SB), NOSPLIT, $0
	MOVD state+0(FP), R0

	MOVW 0(R0), R1
	MOVW 4(R0), R2
	MOVW 8(R0), R3
	MOVW 12(R0), R4
	MOVW 16(R0), R5
	MOVW 20(R0), R6
	MOVW 24(R0), R7
	MOVW 28(R0), R8
	MOVW 32(R0), R9
	MOVW 36(R0), R10
	MOVW 40(R0), R11
	MOVW 44(R0), R12

	XOODOO_ROUND(0x058)
	XOODOO_ROUND(0x038)
	XOODOO_ROUND(0x3c0)
	XOODOO_ROUND(0x0d0)
	XOODOO_ROUND(0x120)
	XOODOO_ROUND(0x014)
	XOODOO_ROUND(0x060)
	XOODOO_ROUND(0x02c)
	XOODOO_ROUND(0x380)
	XOODOO_ROUND(0x0f0)
	XOODOO_ROUND(0x1a0)
	XOODOO_ROUND(0x012)

	MOVD state+0(FP), R0
	MOVW R1, 0(R0)
	MOVW R2, 4(R0)
	MOVW R3, 8(R0)
	MOVW R4, 12(R0)
	MOVW R5, 16(R0)
	MOVW R6, 20(R0)
	MOVW R7, 24(R0)
	MOVW R8, 28(R0)
	MOVW R9, 32(R0)
	MOVW R10, 36(R0)
	MOVW R11, 40(R0)
	MOVW R12, 44(R0)
	RET
